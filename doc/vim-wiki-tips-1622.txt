*vim-wiki-tips-1622.txt*   Change cursor shape in different modes *vwt-1622*

Tip 1622 [https://vim.fandom.com/wiki/Change_cursor_shape_in_different_modes]   Previous |vwt-1621| Next |vwt-1623|

created 
[https://vim.fandom.com/wiki/Vim_Tips_Wiki:New_tips/200903#Change_cursor_shape_in_different_modes] 
March 28, 2009 · complexity basic · author Weltensegler · version 7.0

==============================================================================

To change the shape of the cursor in different modes, you can add the 
following into your vimrc:

FOR TERMINAL ON MACOS  *vwt-1622-for-terminal-on-macos*

>
    "Mode Settings
    
    let &t_SI.="\e[5 q" "SI = INSERT mode
    let &t_SR.="\e[4 q" "SR = REPLACE mode
    let &t_EI.="\e[1 q" "EI = NORMAL mode (ELSE)
    
    "Cursor settings:
    
    "  1 -> blinking block
    "  2 -> solid block 
    "  3 -> blinking underscore
    "  4 -> solid underscore
    "  5 -> blinking vertical bar
    "  6 -> solid vertical bar
<

FOR ITERM2 ON OS X  *vwt-1622-for-iterm2-on-os-x*

>
    let &t_SI = "\<Esc>]50;CursorShape=1\x7"
    let &t_SR = "\<Esc>]50;CursorShape=2\x7"
    let &t_EI = "\<Esc>]50;CursorShape=0\x7"
<

FOR TMUX RUNNING IN ITERM2 ON OS X  *vwt-1622-for-tmux-running-in-iterm2-on-os-x*

>
    let &t_SI = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=1\x7\<Esc>\\"
    let &t_SR = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=2\x7\<Esc>\\"
    let &t_EI = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=0\x7\<Esc>\\"
<

In theory, the escape sequence could be used for tmux in any terminal.

NOTE: These do not work with the default terminal.

FOR KONSOLE IN KDE4  *vwt-1622-for-konsole-in-kde4*

>
    let &t_SI = "\<Esc>]50;CursorShape=1\x7"
    let &t_SR = "\<Esc>]50;CursorShape=2\x7"
    let &t_EI = "\<Esc>]50;CursorShape=0\x7"
<

This works with multiple tabs and windows.

FOR VTE COMPATIBLE TERMINALS (URXVT, ST, XTERM, GNOME-TERMINAL 3.X, KONSOLE KDE5 AND OTHERS) AND WSLTTY  *vwt-1622-for-vte-compatible-terminals-(urxvt,-st,-xterm,-gnome-terminal-3.x,-konsole-kde5-and-others)-and-wsltty*

>
    let &t_SI = "\<Esc>[6 q"
    let &t_SR = "\<Esc>[4 q"
    let &t_EI = "\<Esc>[2 q"
<

Set IBeam shape in insert mode, underline shape in replace mode and block 
shape in normal mode.

FOR THE GNOME-TERMINAL (VERSION 2.26)  *vwt-1622-for-the-gnome-terminal-(version-2.26)*This makes your cursor change in all open terminals. Upgrade your terminal and use the version above instead.

>
    if has("autocmd")
      au InsertEnter * silent execute "!gconftool-2 --type string --set /apps/gnome-terminal/profiles/Default/cursor_shape ibeam"
      au InsertLeave * silent execute "!gconftool-2 --type string --set /apps/gnome-terminal/profiles/Default/cursor_shape block"
      au VimLeave * silent execute "!gconftool-2 --type string --set /apps/gnome-terminal/profiles/Default/cursor_shape ibeam"
    endif
<

FOR THE GNOME-TERMINAL (VERSION 3.X)  *vwt-1622-for-the-gnome-terminal-(version-3.x)*

First make a script gnome-terminal-cursor-shape.sh

>
    #!/bin/sh
    DEFAULTPROF=`dconf read /org/gnome/terminal/legacy/profiles:/default`
    DEFAULTPROF=`echo "$DEFAULTPROF" | sed -e "s/^'/:/" -e "s/'$//"`
    dconf write /org/gnome/terminal/legacy/profiles:/$DEFAULTPROF/cursor-shape "'$1'"
<

Make it executable & put it in /usr/local/binNext, add this to .vimrc

>
    if has("autocmd")
        au InsertEnter *
            \ if v:insertmode == 'i' |
            \   silent execute "!gnome-terminal-cursor-shape.sh ibeam" |
            \ elseif v:insertmode == 'r' |
            \   silent execute "!gnome-terminal-cursor-shape.sh underline" |
            \ endif
        au InsertLeave * silent execute "!gnome-terminal-cursor-shape.sh block"
        au VimLeave * silent execute "!gnome-terminal-cursor-shape.sh block"
    endif
<

If you use more than one profile in gnome-terminal, you might have to adapt 
this to your profiles.

FOR THE GNOME-TERMINAL (VERSION ≥3.16)  *vwt-1622-for-the-gnome-terminal-(version-≥3.16)*

This method has the advantage compared to the method above that it influences 
the given tab only, and not all the tabs that use the given profile. Add the 
following into .vimrc

>
    if has("autocmd")
      au VimEnter,InsertLeave * silent execute '!echo -ne "\e[2 q"' | redraw!
      au InsertEnter,InsertChange *
        \ if v:insertmode == 'i' | 
        \   silent execute '!echo -ne "\e[6 q"' | redraw! |
        \ elseif v:insertmode == 'r' |
        \   silent execute '!echo -ne "\e[4 q"' | redraw! |
        \ endif
      au VimLeave * silent execute '!echo -ne "\e[ q"' | redraw!
    endif
<

Above snippet disables cursor blinking. If you want cursor blink, just 
decrement all the 3 numbers above 2,4,6 by one each.

>
    if has("autocmd")
      au VimEnter,InsertLeave * silent execute '!echo -ne "\e[1 q"' | redraw!
      au InsertEnter,InsertChange *
        \ if v:insertmode == 'i' | 
        \   silent execute '!echo -ne "\e[5 q"' | redraw! |
        \ elseif v:insertmode == 'r' |
        \   silent execute '!echo -ne "\e[3 q"' | redraw! |
        \ endif
      au VimLeave * silent execute '!echo -ne "\e[ q"' | redraw!
    endif
<

FOR XFCE-TERMINAL  *vwt-1622-for-xfce-terminal*

>
    if has("autocmd")
      au InsertEnter * silent execute "!sed -i.bak -e 's/TERMINAL_CURSOR_SHAPE_BLOCK/TERMINAL_CURSOR_SHAPE_UNDERLINE/' ~/.config/xfce4/terminal/terminalrc"                                                                                          
      au InsertLeave * silent execute "!sed -i.bak -e 's/TERMINAL_CURSOR_SHAPE_UNDERLINE/TERMINAL_CURSOR_SHAPE_BLOCK/' ~/.config/xfce4/terminal/terminalrc"                                                                                          
      au VimLeave * silent execute "!sed -i.bak -e 's/TERMINAL_CURSOR_SHAPE_UNDERLINE/TERMINAL_CURSOR_SHAPE_BLOCK/' ~/.config/xfce4/terminal/terminalrc"  
    endif
<    

SEE ALSO  *vwt-1622-see-also*
    - Configuring the cursor [https://vim.fandom.com/wiki/Configuring_the_cursor] 


COMMENTS  *vwt-1622-comments*

VTE compatible terminals includes mate-terminal 1.18.1.

In a vim_use thread on 31 March 2009, Matt Wozniski made the following 
comments 
[http://groups.google.com/group/vim_use/browse_thread/thread/91619df7d7f3fdc5]:

Re the Gnome code: Unless I'm misunderstanding something, this would not only 
work improperly when combined with tools like screen and dvtm, but also, when 
you have multiple terminals open, it would change the cursor shape in all of 
them!

Re the KDE4 code: That won't play nicely with gnu screen, or with dvtm, or 
any of the other terminal multiplexers out there.

Both will probably have problems with multiple tabs.

==============================================================================

In konsole 18.12.3 the above "KDE4" code works AFAICT. In multiple konsole 
tabs, Vim in one tab has no influence on Vim in another tab, which is correct. 
In multiple Vim tabs, cursor shape changes for all tabs, which agrees with the 
--INSERT--, --REPLACE-- or empty label in the command-line area. (FWIW I don't 
use any of those terminal multiplexers.) Tonymec 
[https://vim.fandom.com/wiki/User:Tonymec] (talk 
[https://vim.fandom.com/wiki/User_talk:Tonymec]) 22:40, May 28, 2019 (UTC)

 vim:tw=78:et:ft=help:norl: